buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

import groovy.json.*

configurations {
    compile
    deployJars
}

apply plugin: "forge"
apply plugin: "maven"

group = "net.doubledoordev.pay2spawn"
version = "2.7.1"

targetCompatibility = 1.7
sourceCompatibility = 1.7

archivesBaseName = 'Pay2Spawn'
minecraft {
    version = "1.7.10-10.13.2.1286"
    runDir = "jars"
}

repositories {
    maven {
         name "DDD repo"
         url "http://doubledoordev.net/maven/"
     }
}

dependencies {
    compile "net.doubledoordev.d3core:D3Core:" + project.minecraft.version + "-+:dev"
}

if (System.getenv().BUILD_NUMBER != null) version += "." + System.getenv().BUILD_NUMBER
def builder = new groovy.json.JsonBuilder()
builder (version: version, mcversion: project.minecraft.version, apiversion: project.minecraft.apiVersion)
new File("versions.json").write(builder.toPrettyString())

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
    }
}

task sourcesJar(type: Jar) {
    from "LICENSE.txt"
    from sourceSets.main.allSource
    classifier = 'src'
    appendix = project.minecraft.version
    manifest {
        attributes 'FMLAT': 'P2S_at.cfg'
    }
}

task deobfJar(type: Jar) {
    from "LICENSE.txt"
    from sourceSets.main.output
	classifier = 'dev'
    appendix = project.minecraft.version
    manifest {
        attributes 'FMLAT': 'P2S_at.cfg'
    }
}

jar {
    from "LICENSE.txt"
    appendix = project.minecraft.version
    manifest {
        attributes 'FMLAT': 'P2S_at.cfg'
    }
}

artifacts {
	archives jar
    archives sourcesJar
    archives deobfJar
}

uploadArchives {
    if (project.hasProperty("dddUser") && project.hasProperty("dddPass")) {
        repositories {
            mavenDeployer {
                repository(url: "http://doubledoordev.net:8082/artifactory/mods") {
                    authentication(userName: dddUser, password: dddPass)
                }
                pom {
                    groupId = project.group
                    version = project.minecraft.version + "-" + project.version
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging 'jar'
                        description 'Pay2Spawn'
                        url 'https://github.com/CCM-Modding/Pay2Spawn'

                        scm {
                            url 'https://github.com/CCM-Modding/Pay2Spawn'
                            connection 'scm:git:git://github.com/CCM-Modding/Pay2Spawn.git'
                            developerConnection 'scm:git:git@github.com:CCM-Modding/Pay2Spawn.git'
                        }

                        issueManagement {
                            system 'github'
                            url 'https://github.com/CCM-Modding/Pay2Spawn/issues'
                        }

                        licenses  {
                            license  {
                                name 'MIT License'
                                url 'https://raw.github.com/CCM-Modding/Pay2Spawn/master/LICENCE'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'Dries007'
                                name 'Dries007'
                                roles { role 'developer' }
                            }
                            developer {
                                id 'Claycorp'
                                name 'Claycorp'
                                roles { role 'architect' }
                            }
                        }
                    }
                }
            }
        }
    }
}
